use mini_c_ast::{
    FunctionCallExpression,
    BinaryExpression,
    UnaryExpression,
    BinaryOperator,
    UnaryOperator,
    VariableDeclarationItem,
    IfStatement,
    ForStatement,
    WhileStatement,
    AssignmentExpression,
    ArrayIndexingExpression,
    Declaration,
    Expression,
    FunctionDeclaration,
    Ident,
    Parameter,
    Program,
    ReturnStatement,
    Scope,
    Span,
    Spanned,
    Statement,
    Type,
    Value,
    VariableDeclaration,
};
use std::str::FromStr;
use mini_c_lexer::Token;
use crate::{error, MiniCParseError};

grammar<'input>(input: &'input str);

pub Program: Program =
    <preamble:Preamble?> <declarations:Declaration*> =>
        Program { preamble: preamble.is_some(), declarations};

Preamble: () = "#include" "minicio.h" => ();

Declaration: Declaration = {
    VariableDeclaration => Declaration::Variable(<>),
    FunctionDeclaration => Declaration::Function(<>),
};
VariableDeclaration: VariableDeclaration = <Type> <ListOf<VariableDeclarationItem>> ";" => VariableDeclaration(<>);
VariableDeclarationItem: VariableDeclarationItem = {
    Ident => VariableDeclarationItem::Normal(<>),
    <Ident> "[" <Int> "]" => VariableDeclarationItem::Array(<>),
};
FunctionDeclaration: FunctionDeclaration = {
    <Type> <Ident> "(" <ListOf<Parameter>> ")" <ScopeOrSemicolon> => FunctionDeclaration(<>)
};

Parameter: Parameter = Type Ident => Parameter(<>);
Scope: Scope = "{" <VariableDeclaration*> <Statement*> "}" => Scope(<>);

Statement: Statement = {
    IfStatement => Statement::If(<>),
    ForStatement => Statement::For(<>),
    WhileStatement => Statement::While(<>),
    BreakStatement => Statement::Break,
    ReturnStatement => Statement::Return(<>),
    Scope => Statement::Scope(<>),
};
IfStatement: IfStatement = {
    "if" "(" <Expression> ")" <Scope> <("else" <Scope>)?> => IfStatement(<>),
};
ForStatement: ForStatement = "for" "(" <(<Expression?> "," <Expression?> "," <Expression?>)> ")" <Scope> => ForStatement(<>);
WhileStatement: WhileStatement = "while" "(" <Expression?> ")" <Scope> => WhileStatement(<>);
BreakStatement: () = "break" ";" => ();
ReturnStatement: ReturnStatement = "return" <Expression?> ";" => ReturnStatement(<>);

Expression = ExpressionPrecedenceStart;
// // Precedence:
// 9. =
// 8. ||
// 7. &&
// 6. !
// 5. == != < < = > > =
// 4. + binary -
// 3. * /
// 2. unary -
// 1. 10, true, a, a[i] f()  (expr)
ExpressionPrecedenceStart = AssignmentExpression;

AssignmentExpression = {
    <Ident> "=" <AssignmentExpression> => Expression::Assignment(box AssignmentExpression(<>)),
    OrExpression,
};
OrExpression = BinaryOperationLevel<OrOperator, AndExpression>;
AndExpression = BinaryOperationLevel<AndOperator, NotExpression>;
NotExpression = UnaryOperationLevel<NotOperator, ComparisonExpression>;
ComparisonExpression = BinaryOperationLevel<ComparisonOperator, TermExpression>;
TermExpression = BinaryOperationLevel<TermOperator, FactorExpression>;
FactorExpression = BinaryOperationLevel<FactorOperator, NegationExpression>;
NegationExpression = UnaryOperationLevel<NegationOperator, PrimaryExpression>;

PrimaryExpression: Expression = {
    "(" <Expression> ")",
    ValueExpression => Expression::Value(<>),
    Ident => Expression::Variable(<>),
    ArrayIndexingExpression => Expression::ArrayIndexing(box <>),
    FunctionCallExpression => Expression::FunctionCall(<>),
};
ArrayIndexingExpression: ArrayIndexingExpression = <Ident> "[" <Expression> "]" => ArrayIndexingExpression(<>);
FunctionCallExpression: FunctionCallExpression = <Ident> "(" <ListOf<Expression>> ")" => FunctionCallExpression(<>);

OrOperator: BinaryOperator = "||" => BinaryOperator::Or;
AndOperator: BinaryOperator = "&&" => BinaryOperator::And;
NotOperator: UnaryOperator = "!" => UnaryOperator::Not;
ComparisonOperator: BinaryOperator = {
    "==" => BinaryOperator::Equals,
    "!=" => BinaryOperator::NotEquals,
    "<" => BinaryOperator::Less,
    "<=" => BinaryOperator::LessOrEquals,
    ">" => BinaryOperator::Greater,
    ">=" => BinaryOperator::GreaterOrEquals,
};
TermOperator: BinaryOperator = {
    "+" => BinaryOperator::Add,
    "-" => BinaryOperator::Sub,
};
FactorOperator: BinaryOperator = {
    "*" => BinaryOperator::Mul,
    "/" => BinaryOperator::Div,
};
NegationOperator: UnaryOperator = "-" => UnaryOperator::Negative;

ValueExpression: Value = {
    Int => Value::Int(<>),
    Bool => Value::Bool(<>),
};
Int: i64 = "{int}" =>? i64::from_str(<>)
    .map_err(|err| error(MiniCParseError::InvalidInteger(<>.to_owned(), err)));
Bool: bool = {
    "true" => true,
    "false" => false,
};

Type: Type = {
    "{ident}" =>? {
        match <> {
            "void" => Ok(Type::Void),
            "bool" => Ok(Type::Bool),
            "int" => Ok(Type::Int),
            _ => Err(error(MiniCParseError::InvalidType(<>.into()))),
        }
    },
};

ScopeOrSemicolon: Option<Scope> = {
    Scope => Some(<>),
    ";" => None,
};

Ident: Ident = "{ident}" => <>.to_owned();

// Macro: comma separated list of something
ListOf<T>: Vec<T> = {
    <mut list:(<T> ",")*> <last_element:T?> => match last_element {
        None => list,
        Some(last_element) => {
            list.push(last_element);
            list
        }
    }
};

// Macro: precedence level definition macro for binary expressions
BinaryOperationLevel<Operator, NextLevel>: Expression = {
    BinaryOperationLevel<Operator,NextLevel> Operator NextLevel => Expression::Binary(box BinaryExpression(<>)),
    NextLevel,
};

// Macro: precedence level definition macro for unary expressions
UnaryOperationLevel<Operator, NextLevel>: Expression = {
    <op:Operator> <r:UnaryOperationLevel<Operator,NextLevel>> => Expression::Unary(box UnaryExpression(<>)),
    NextLevel,
};

// Macro: rule with span, requires the Rule name to be the same as the type
Spanned<Rule>: Spanned<Rule> = <l:@L> <inner:Rule> <r:@R> =>
    Spanned {
        inner,
        span: Span::new(l, r)
    };


extern {
    type Location = usize;
    type Error = MiniCParseError;

    enum Token<'input> {
        "{ident}" => Token::Ident(<&'input str>),
        "{int}" => Token::Int(<&'input str>),
        "#include" => Token::IncludeMinicio,
        "minicio.h" => Token::MinicioHeader,
        "if" => Token::If,
        "else" => Token::Else,
        "for" => Token::For,
        "while" => Token::While,
        "break" => Token::Break,
        "return" => Token::Return,
        "true" => Token::True,
        "false" => Token::False,
        ";" => Token::SemiColon,
        "," => Token::Comma,
        "=" => Token::SingleEquals,
        "(" => Token::OpenParenthesis,
        ")" => Token::CloseParenthesis,
        "{" => Token::OpenBraces,
        "}" => Token::CloseBraces,
        "[" => Token::OpenBrackets,
        "]" => Token::CloseBrackets,
        "+" => Token::Plus,
        "-" => Token::Minus,
        "*" => Token::Star,
        "/" => Token::Slash,
        "&&" => Token::And,
        "||" => Token::Or,
        "!" => Token::Not,
        "<" => Token::Less,
        "<=" => Token::LessOrEqual,
        "==" => Token::DoubleEquals,
        "!=" => Token::NotEquals,
        ">" => Token::Greater,
        ">=" => Token::GreaterOrEqual,
    }
}
